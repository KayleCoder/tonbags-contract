#include "imports/stdlib.fc";
#include "constants.fc";
#include "messages.fc";
#include "params.fc";
#include "utils.fc";

{-
    storage#_
        admin_address:MsgAddress bag_storage_contracts:^Cell storage_contract_code:^Cell = Storage;
-}
(slice, cell, cell) load_data() inline {
    slice ds = get_data().begin_parse();
    return (
        ds~load_msg_addr(),
        ds~load_dict(),
        ds~load_ref()
    );
}

() save_data(slice admin_address, cell bag_storage_contracts, cell storage_contract_code) impure inline {
    set_data(
        begin_cell()
            .store_slice(admin_address)
            .store_dict(bag_storage_contracts)
            .store_ref(storage_contract_code)
            .end_cell()
    );
}

cell load_bag_storage_contracts_dict() inline {
    (slice admin_address, cell bag_storage_contracts, cell storage_contract_code) = load_data();
    return bag_storage_contracts;
}

() save_bag_storage_contracts_dict(cell bags_dict) impure inline {
    (slice admin_address, cell bag_storage_contracts, cell storage_contract_code) = load_data();
    set_data(
        begin_cell()
            .store_slice(admin_address)
            .store_dict(bags_dict)
            .store_ref(storage_contract_code)
            .end_cell()
    );
}

() deploy_storage_contract(
    cell storage_contract_code, slice owner_address, int query_id,
    int file_size, int merkle_hash, int torrent_hash, int initial_storage_fee
) impure {
    throw_unless(error::file_too_small, file_size >= minimal_file_size);
    throw_unless(error::file_too_big, file_size <= maximal_file_size);

    cell state_init = build_storage_contract_stateinit(
        storage_contract_code, torrent_hash, owner_address, merkle_hash, file_size
    );
    slice storage_contract_address = calculate_storage_contract_address(state_init);

    cell bags_dict = load_bag_storage_contracts_dict();
    var (_, success?) = bags_dict.udict_get?(256, torrent_hash);
    if (success?) {
        throw(error::duplicated_torrent_hash);
    }
    bags_dict.udict_set(256, torrent_hash, storage_contract_address);
    save_bag_storage_contracts_dict(bags_dict);

    cell msg = begin_cell()
            .store_msg_flag(msg_flag::bounceable)
            .store_slice(storage_contract_address)
            .store_coins(0)
            .store_uint(4 + 2, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1)
            .store_ref(state_init)
            ;; .store_uint(op::place_storage_order, 32)
            .store_uint(query_id, 64)
            .end_cell();
    send_raw_message(msg, 64);
}

() recv_internal(int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore empty messages
        return ();
    }
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);

    if (flags & 1) { ;; ignore all bounced messages
        return ();
    }
    slice sender_address = cs~load_msg_addr();

    cs~load_msg_addr(); ;; skip dst
    cs~load_coins(); ;; skip value
    cs~skip_bits(1); ;; skip extracurrency collection
    cs~load_coins(); ;; skip ihr_fee
    int fwd_fee = muldiv(cs~load_coins(), 3, 2); ;; we use message fwd_fee for estimation of forward_payload costs
  
    int op = in_msg_body~load_op();
    int query_id = in_msg_body~load_query_id();

    (slice admin_address, cell bag_storage_contracts, cell storage_contract_code) = load_data();

    if (op == op::update_admin) {
        throw_unless(error::unauthorized, equal_slice_bits(sender_address, admin_address));
        slice new_admin_address = in_msg_body~load_msg_addr();
        save_data(new_admin_address, bag_storage_contracts, storage_contract_code);
        return ();
    }

    if (op == op::place_storage_order) {
        throw_unless(error::not_enough_storage_fee, msg_value >= min_storage_fee);

        ;; torrent_info piece_size:uint32 file_size:uint64 root_hash:(## 256) header_size:uint64 header_hash:(## 256)
        ;;     microchunk_hash:(Maybe (## 256)) description:Text = TorrentInfo;
        ;;
        ;; new_storage_contract#00000001 query_id:uint64 info:(^ TorrentInfo) microchunk_hash:uint256 = NewStorageContract;
        cell torrent_info = in_msg_body~load_ref();
        int torrent_hash = cell_hash(torrent_info);
        slice info_cs = torrent_info.begin_parse();
        info_cs~skip_bits(32);
        int file_size = info_cs~load_uint(64);
        int merkle_hash = in_msg_body~load_uint(256);

        deploy_storage_contract(storage_contract_code, sender_address, query_id, file_size, merkle_hash, torrent_hash, msg_value);

        return ();
    }

    throw(error::unknown_op);
}

slice get_admin_address() method_id {
    (slice admin_address, cell bag_storage_contracts, cell storage_contract_code) = load_data();
    return admin_address;
}

slice get_storage_contract_address(int torrent_hash) method_id {
    cell bags_dict = load_bag_storage_contracts_dict();
    var (storage_contract_address, success?) = bags_dict.udict_get?(256, torrent_hash);
    if (success?) {
        return storage_contract_address;
    }
    return "";
}